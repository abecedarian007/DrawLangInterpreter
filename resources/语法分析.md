# 语法分析

## 原始文法

Program  → ε| Program Statement SEMICOLON

Statement → OriginStatment 
						| ScaleStatment
        				| RotStatment
						| ForStatment

OriginStatment → ORIGIN IS LEFT_BRACKET Expression COMMA Expression RIGHT_BRACKET

ScaleStatment  → SCALE IS LEFT_BRACKET Expression COMMA Expression RIGHT_BRACKET

RotStatment → ROT IS Expression

ForStatment → FOR T FROM Expression TO  Expression STEP Expression 
							DRAW LEFT_BRACKET Expression COMMA Expression RIGHT_BRACKET

Expression → Expression PLUS Expression
						| Expression MINUS Expression
						| Expression MUL Expression
						| Expression DIV Expression
						| PLUS Expression
						| MINUS Expression
						| Expression POWER Expression
						| CONST_ID
						| T
						| FUNCTION LEFT_BRACKET Expression RIGHT_BRACKET
						| LEFT_BRACKET Expression RIGHT_BRACKET

## 无二义/无左递归文法

Program   → Statement SEMICOLON Program |ε 

Statement → OriginStatment 
						| ScaleStatment
        				| RotStatment
						| ForStatment

OriginStatment → ORIGIN IS LEFT_BRACKET Expression COMMA Expression RIGHT_BRACKET

ScaleStatment  → SCALE IS LEFT_BRACKET Expression COMMA Expression RIGHT_BRACKET

RotStatment → ROT IS Expression

ForStatment → FOR T FROM Expression TO  Expression STEP Expression 
							DRAW LEFT_BRACKET Expression COMMA Expression RIGHT_BRACKET

Expression → Term Expression'

Expression'→ PLUS Term Expression' | MINUS Term Expression' |ε

Term → Factor Term'

Term'   → MUL Factor Term' | DIV Factor Term' |ε

Factor   → PLUS Factor | MINUS Factor | Component

Component → Atom POWER Component | Atom

Atom → CONST_ID
				| T
				| FUNCTION LEFT_BRACKET Expression RIGHT_BRACKET
				| LEFT_BRACKET Expression RIGHT_BRACKET

## EBNF产生式

Program → { Statement SEMICOLON } 

Statement → OriginStatment
						| ScaleStatment
						| RotStatment
						| ForStatment

OriginStatment → ORIGIN IS LEFT_BRACKET Expression COMMA Expression RIGHT_BRACKET

ScaleStatment → SCALE IS LEFT_BRACKET Expression COMMA Expression RIGHT_BRACKET

RotStatment → ROT IS Expression

ForStatment → FOR T FROM Expression TO  Expression STEP Expression 
							DRAW LEFT_BRACKET Expression COMMA Expression RIGHT_BRACKET

ColorStatement → COLOR IS Expression
                            | COLOR IS LEFT_BRACKET Expression COMMA Expression COMMA Expression RIGHT_BRACKET

SetCircleStatement → SET_CIRCLE LEFT_BRACKET Expression COMMA Expression RIGHT_BRACKET
                                    RADIUS LEFT_BRACKET Expression COMMA Expression RIGHT_BRACKET;

Expression  → Color | Term  { ( PLUS | MINUS ) Term } 

Term → Factor { ( MUL  | DIV ) Factor }

Factor → ( PLUS | MINUS ) Factor | Component

Component  → Atom [ POWER Component ]

Atom → CONST_ID
				| T
				| FUNCTION LEFT_BRACKET Expression RIGHT_BRACKET
				| LEFT_BRACKET Expression RIGHT_BRACKET 